apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'
apply plugin: 'maven-publish'

def publishName = 'filesystem'
group = 'com.cisco.gradle'
version = '1.1'

repositories {
    mavenCentral()
}

dependencies {
    compile localGroovy()
    compile gradleApi()
    testCompile gradleTestKit()
    testCompile "org.spockframework:spock-core:1.0-groovy-2.4"
    testCompile "org.codehaus.groovy:groovy-all:2.4.7"
}

test {
    testLogging {
        exceptionFormat = 'full'
    }
}

ext {
    if (!project.hasProperty('publishUrl')) publishUrl = null
    if (!project.hasProperty('publishUser')) publishUser = null
    if (!project.hasProperty('publishPassword')) publishPassword = null
}

targetCompatibility = '1.7'

publishing {
    repositories {
        maven {
            url = publishUrl
            credentials {
                username publishUser
                password publishPassword
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = publishName
        }
    }
}

gradle.taskGraph.whenReady { graph ->
    if (graph.hasTask(':publish') && !publishUrl) {
        throw new GradleException('To publish, you must pass in -PpublishUrl (and optionally publishUser/publishPassword).')
    }
}
